       IDENTIFICATION DIVISION.
       PROGRAM-ID.  V1UPDATE.

       ENVIRONMENT  DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

             SELECT MASTER-FILE-INDEXED ASSIGN TO DA-I-MASTIO

                    ORGANIZATION IS INDEXED
                    ACCESS IS RANDOM
                    RECORD KEY IS MASTER-NUM-INDEXED.

             SELECT TRANS-FILE-IN  ASSIGN TO UT-S-TRIN.

             SELECT TRANSACTION-LIST-REPORT ASSIGN TO UT-S-TROUT.

       DATA DIVISION.
       FILE SECTION.

       FD  MASTER-FILE-INDEXED.


       01  MASTER-REC-INDEXED.
           05  MASTER-NUM-INDEXED          PIC X(5).
           05  FILLER                      PIC XX.
           05  NAME-INDEXED.
               10 F-INIT-INDEXED           PIC X.
               10 M-INIT-INDEXED           PIC X.
               10 LAST-INDEXED             PIC X(13).
           05  TRANS-CUR-SALES-INDEXED     PIC S9(5)V99.
           05  FILLER                      PIC XX.
           05  TRANS-CUR-COMM-INDEXED      PIC S9(5)V99.
           05  FILLER                      PIC XX.
           05  TRANS-YTD-SALES-INDEXED     PIC S9(6)V99.
           05  FILLER                      PIC XX.
           05  TRANS-YTD-COMM-INDEXED      PIC S9(6)V99.
           05  FILLER                      PIC X(22).




       FD  TRANS-FILE-IN RECORDING MODE IS F.


       01  TRANS-REC-IN.
           05  TRANS-NUM-IN                    PIC X(5).
           05  TRANS-SALES-DATA-IN.
               10  FILLER                      PIC XX.
               10  NAME-IN.
                   15 F-INIT-IN                PIC X.
                   15 M-INIT-IN                PIC X.
                   15 LAST-IN                  PIC X(13).
               10  TRANS-CURRENT-SALES         PIC S9(5)V99.
               10  FILLER                      PIC XX.
               10  TRANS-CURRENT-COMMISION     PIC S9(5)V99.
               10  FILLER                      PIC XX.

           05  TRANS-CODE-IN                   PIC X.
                   88  UPDATE-MASTER               VALUE 'A'.
                   88  CREATE-NEW-RECORD           VALUE 'B'.
                   88  DELETE-SALESMAN             VALUE 'C'.
           05  FILLER                          PIC X(39).

       FD  TRANSACTION-LIST-REPORT RECORDING MODE IS F.
       01  REPORT-LINE-OUT                     PIC X(132).



       WORKING-STORAGE SECTION.
       01  WORKING-FIELDS.
           05  EOF-TRANS-WS                    PIC X(3) VALUE 'NO '.


       01  TRANSACTION-OUTPUT-LINE.
           02                                  PIC X VALUE SPACE.
           02  TEL-RECORD                      PIC X(80).
           02                                  PIC X VALUE SPACE.
           02  TEL-MESSAGE                     PIC X(50).




       PROCEDURE DIVISION.

       100-MAIN-MODULE.
           PERFORM 200-OPEN.
           PERFORM 300-PROCESS
               UNTIL EOF-TRANS-WS = 'YES'.
           PERFORM 900-CLOSE.
           STOP RUN.

       200-OPEN.


           OPEN INPUT TRANS-FILE-IN
                I-O   MASTER-FILE-INDEXED
                OUTPUT TRANSACTION-LIST-REPORT.

           READ TRANS-FILE-IN
               AT END MOVE 'YES' TO EOF-TRANS-WS
           END-READ.

       300-PROCESS.


           EVALUATE TRUE
               WHEN CREATE-NEW-RECORD
                   PERFORM 400-NEW-RTN

               WHEN DELETE-SALESMAN
                   PERFORM 500-DELETE-RTN

               WHEN UPDATE-MASTER
                   PERFORM 600-UPDATE-RTN

               WHEN OTHER
                   MOVE "BAD CODE ENTERED"
                   TO TEL-MESSAGE
                   PERFORM 700-PRINT-ERROR

            END-EVALUATE.



           READ TRANS-FILE-IN
               AT END MOVE 'YES' TO EOF-TRANS-WS
           END-READ.


       400-NEW-RTN.

           MOVE TRANS-NUM-IN TO MASTER-NUM-INDEXED

           MOVE NAME-IN  TO  NAME-INDEXED

           MOVE TRANS-CURRENT-SALES TO TRANS-CUR-SALES-INDEXED

           MOVE TRANS-CURRENT-COMMISION TO TRANS-CUR-COMM-INDEXED.

           MOVE TRANS-CURRENT-SALES TO TRANS-YTD-SALES-INDEXED

           MOVE TRANS-CURRENT-COMMISION TO TRANS-YTD-COMM-INDEXED.


            WRITE MASTER-REC-INDEXED
               INVALID KEY
                   MOVE "ATTEMPTED ADDITION OF EXISTING RECORD"
                   TO  TEL-MESSAGE
                   PERFORM 700-PRINT-ERROR

               NOT INVALID KEY
                   MOVE "SUCESSFUL ADDITION"
                   TO TEL-MESSAGE
                   PERFORM 700-PRINT-ERROR

           END-WRITE.


       500-DELETE-RTN.
           MOVE TRANS-NUM-IN TO MASTER-NUM-INDEXED.
           READ MASTER-FILE-INDEXED
                INVALID KEY
                    MOVE "ATTEMPTED DELETION OF NON-EXISTING RECORD"
                    TO  TEL-MESSAGE
                    PERFORM 700-PRINT-ERROR

                 NOT INVALID KEY
                     DELETE MASTER-FILE-INDEXED

                         INVALID KEY
                             MOVE "BAD DELETION LOGIC"
                             TO TEL-MESSAGE
                             PERFORM 700-PRINT-ERROR

                         NOT INVALID KEY
                             MOVE "SUCCESSFUL DELETION"
                             TO TEL-MESSAGE
                             PERFORM 700-PRINT-ERROR

                     END-DELETE
           END-READ.

       600-UPDATE-RTN.

           MOVE TRANS-NUM-IN TO MASTER-NUM-INDEXED.

           READ MASTER-FILE-INDEXED

               INVALID KEY
                 MOVE "ATTEMPTED CHANGE/UPDATE OF NON-EXISTING RECORD"
                  TO TEL-MESSAGE
                   PERFORM 700-PRINT-ERROR

               NOT INVALID KEY PERFORM 800-REWRITE-RTN

           END-READ.



       800-REWRITE-RTN.
           MOVE NAME-IN TO NAME-INDEXED
           MOVE  TRANS-CURRENT-SALES
                 TO    TRANS-CUR-SALES-INDEXED
           MOVE  TRANS-CURRENT-COMMISION
                 TO TRANS-CUR-COMM-INDEXED
           ADD   TRANS-CURRENT-SALES
                 TO   TRANS-YTD-SALES-INDEXED
           ADD    TRANS-CURRENT-COMMISION
                 TO     TRANS-YTD-COMM-INDEXED


           REWRITE MASTER-REC-INDEXED

               INVALID KEY
                   MOVE "REWRITE LOGIC ERROR"
                   TO TEL-MESSAGE
                   PERFORM 700-PRINT-ERROR

                NOT INVALID KEY
                   MOVE "SUCESSFUL CHANGE"
                   TO TEL-MESSAGE
                   PERFORM 700-PRINT-ERROR

           END-REWRITE.

       700-PRINT-ERROR.

           MOVE TRANS-REC-IN TO TEL-RECORD.

           WRITE REPORT-LINE-OUT FROM TRANSACTION-OUTPUT-LINE
                AFTER ADVANCING 2 LINES.



       900-CLOSE.
           CLOSE TRANS-FILE-IN
                 MASTER-FILE-INDEXED
                 TRANSACTION-LIST-REPORT.

